#!/usr/bin/perl

use strict;
use warnings;
use 5.010;

use JSON;
use Path::Tiny;
use Template;
use MetaCPAN::Client;
use Data::Dumper;
use FindBin '$RealBin';

my $global_cfg = decode_json(path('dashboard.json')->slurp);

my $tt = Template->new({
  INCLUDE_PATH => $global_cfg->{input_dir},
  OUTPUT_PATH  => $global_cfg->{output_dir},
  WRAPPER      => $global_cfg->{wrapper},
});

my @authors;

for (glob "$RealBin/authors/*.json") {
  push @authors, do_author($tt, $_, $global_cfg);
}

$tt->process($global_cfg->{index_template}, { authors => \@authors }, 'index.html');

for (@{ $global_cfg->{page_templates} }) {
  $tt->process("$_.tt", {}, "$_/index.html");
}

sub do_author {
  my ($tt, $file, $global_cfg) = @_;

  my $cfg = decode_json(path($file)->slurp);

  $cfg->{modules} = [];

  my $mcpan    = MetaCPAN::Client->new;
  my $author   = $mcpan->author($cfg->{author}{cpan});
  my $releases = $author->releases;

  while ( my $rel = $releases->next ) {
    my $mod;
    $mod->{name} = $rel->name;
    $mod->{dist} = $rel->distribution;
    $mod->{date} = (split /T/, $rel->date)[0];
    # Get the repo link.
    # 1. It should be in the "web" key
    # 2. Otherwise, check the "url" key
    # 3. Finally, synthesise one (that might well be wrong)
    $mod->{repo} = $rel->resources->{repository}{web}
      // $rel->resources->{repository}{url}
      // lc "https://github.com/$cfg->{author}{github}/$mod->{dist}";
    # We need the repo's name. Try to extract it from the URL
    if ($mod->{repo} =~ /^http/) {
      ($mod->{repo_name}) = $mod->{repo} =~ /([-\w]+)$/;
    } elsif ($mod->{repo} =~ /^git/) {
      ($mod->{repo_name}) = $mod->{repo} =~ /([-\w]+)\.git$/;
    } else {
      warn "Strange repo for $mod->{name} ($mod->{repo}). Skipping.\n";
      next;
    }
    ($mod->{repo_owner}) = $mod->{repo} =~ m[([-\w]+)/$mod->{repo_name}];
    push @{ $cfg->{modules} }, $mod;
  }

  $cfg->{modules} = [ sort { $a->{name} cmp $b->{name} } @{$cfg->{modules}} ];

  $cfg->{sort} //= {};
  $cfg->{sort}{column} //= 0;
  $cfg->{sort}{column} = 2 if 'date' eq lc $cfg->{sort}{column};
  $cfg->{sort}{direction} //= 'asc';

  $cfg = { %$cfg, %$global_cfg, badge_link => \&badge_link };

  $tt->process($cfg->{author_template}, $cfg, "$cfg->{author}{cpan}/index.html")
    or die $tt->error;

  return $cfg->{author}{cpan};
}


sub badge_link {
  my ($link_url, $img_url, $alt_text) = @_;

  return qq[<a href="$link_url"><img class="backup_picture" alt="$alt_text" src="$img_url"></a>];
}
